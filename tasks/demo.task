# demo.task


# ======================= SCENE 1 =========================
# Render sprites and demonstrate movement in 640x360.
# =========================================================  

# Initialization:
  
ECHO OFF
RUN _CLEAR
RUN _TERM_RESOLUTION 320 200


# Global Variables:

SET $X = 40
SET $DX = 0
SET $Y = 140
SET $VX = 2
SET $DT = 0.0
SET $SPRITE_PATHS = {"./tasks/assets/char_1_1.png",
                     "./tasks/assets/char_1_2.png"}
                     
SET $TILE_PATHS = {"./tasks/assets/tile001.png",
                   "./tasks/assets/tile002.png"}
SET $SPRITES = {}
SET $TILES = {}
SET $MOUSE_EVENTS = {}
SET $SPRITE_OFFSET = 20

# ====================== FUNCTIONS ========================

FUNCTION FCNMOVE($X, $Y, $POSARRAY):
  
  SET $POSARRAY[0] = $POSARRAY[0] + $X
  SET $POSARRAY[1] = $POSARRAY[1] + $Y
  
  RETURN $POSARRAY
END

# ======================== MAIN ===========================


# Preload sprite assets into indexed array for reuse:

FOR ($i=0; $i<LEN($SPRITE_PATHS); $i++):
  RUN _TERM_SPRITE_LOAD -file $SPRITE_PATHS[$i] TO $SPRITES[$i]
END

FOR ($i=0; $i<LEN($TILE_PATHS); $i++):
  RUN _TERM_SPRITE_LOAD -file $TILE_PATHS[$i] TO $TILES[$i]
END


# Render Background:

FOR ($i=0;$i<=320;$i=$i+20):
  FOR ($j=0;$j<=200;$j=$j+20):
    IF ($j<=140):
      RUN _TERM_SPRITE -x $i -y $j -sprite $TILES[0] -layer 16
    ELSE
      RUN _TERM_SPRITE -x $i -y $j -sprite $TILES[1] -layer 16
    END
  END
END
RUN _TERM_RENDER -layer 16


# Draw Text:

RUN _TERM_TEXT -x 100 -y 10 -text " --- GAME DEMO --- " -color 18 -layer 10
RUN _TERM_TEXT -x 60 -y 160 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 60 -y 170 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 60 -y 180 -text "|" -color 18 -layer 10
RUN _TERM_TEXT -x 280 -y 160 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 280 -y 170 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 280 -y 180 -text "|FINISH" -color 18 -layer 10
RUN _TERM_RENDER -layer 10


# GAME LOOP:

WHILE ($X<290):

  # Clean the character old position:
    
  IF ($X>40):
    RUN _TERM_CLEAN -x $X-$SPRITE_OFFSET-$VX -y $Y -width 40 -height 40 -layer 8
  END
  
  
  # Draw the character new position:
  
  RUN _TERM_SPRITE -x $X-$SPRITE_OFFSET -y $Y -sprite $SPRITES[0] -layer 8
  RUN _TERM_RENDER -layer 8
  
 
  # Get mouse events
    
  RUN _TERM_MOUSE TO $MOUSE_EVENTS
 
  
  # Update character position:
  
  SET $ERROR = 0
  RUN _CALC ($MOUSE_EVENTS[0] - $X) TO $ERROR
  IF ($ERROR >= 0):
    RUN _CALC fmin($MOUSE_EVENTS[0]-$X, 1) TO $VX
  ELSE
    RUN _CALC fmax($MOUSE_EVENTS[0]-$X, -1) TO $VX
  END
  
  RUN _CALC ($VX * $MOUSE_EVENTS[2]) TO $VX
  
  SET $X = $X + $VX
  
  
  # Cap framerate:
  
  RUN _WAIT 66

END


# Wait

RUN _WAIT 4000


  
# ==================== EXIT CLEANUP =======================


# Clear All Layers:

FOR ($i=1;$i<17;$i++):
  RUN _TERM_CLEAN -x 0 -y 0 -width 640 -height 360 -layer $i
  RUN _TERM_RENDER -layer $i
END


# Press ENTER to exit:

SET $KEY
INPUT $KEY
RUN _TERM_RESOLUTION 640 360
ECHO ON

