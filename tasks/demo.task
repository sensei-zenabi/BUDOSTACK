# demo.task


# ======================= SCENE 1 =========================
# Render sprites and demonstrate movement in 640x360.
# =========================================================  

# Initialization:
  
ECHO OFF
RUN _CLEAR
RUN _TERM_RESOLUTION 320 200


# Global Game Variables:

SET $X = 40
SET $Y = 140
SET $dV = 0
SET $VX = 0
SET $TIME = 0
SET $STRING = "none"
SET $TIMER_STATE = 0
SET $SPRITE_PATHS = {"./tasks/assets/char_1_1.png",
                     "./tasks/assets/char_1_2.png",
                     "./tasks/assets/cloud.png"}
                     
SET $TILE_PATHS = {"./tasks/assets/tile001.png",
                   "./tasks/assets/tile002.png",
                   "./tasks/assets/tile003.png",
                   "./tasks/assets/tile004.png"}
SET $SPRITES = {}
SET $TILES = {}
SET $MOUSE_EVENTS = {}
SET $SPRITE_OFFSET = 20
SET $SPRITE_FRAME = 0

# ====================== FUNCTIONS ========================

# None


# ======================== MAIN ===========================


# LOAD SPRITES INTO MEMORY:

FOR ($i=0; $i<LEN($SPRITE_PATHS); $i++):
  RUN _TERM_SPRITE_LOAD -file $SPRITE_PATHS[$i] TO $SPRITES[$i]
END

FOR ($i=0; $i<LEN($TILE_PATHS); $i++):
  RUN _TERM_SPRITE_LOAD -file $TILE_PATHS[$i] TO $TILES[$i]
END


# CLEAN BACKGROUND LAYER:

RUN _TERM_CLEAN -x 0 -y 0 -width 320 -height 200 -layer 16
 

# RENDER BACKGROUND:

FOR ($i=0;$i<=320;$i=$i+20):
  FOR ($j=0;$j<=200;$j=$j+20):
    IF ($j<80):
      # Draw sky
      RUN _TERM_SPRITE -x $i -y $j -sprite $TILES[0] -layer 16
    END
    IF ($j>=80 AND $j<140):
      # Draw crowd
      RUN _TERM_SPRITE -x $i -y $j -sprite $TILES[3] -layer 16
    END
    IF ($j>=140 AND $j<160):
      # Draw fence
      RUN _TERM_SPRITE -x $i -y $j -sprite $TILES[2] -layer 16
    END
    IF ($j>=160):
      # Draw ground
      RUN _TERM_SPRITE -x $i -y $j -sprite $TILES[1] -layer 16
    END
  END
END

# Draw Clouds

RUN _TERM_SPRITE -x 10 -y 10 -sprite $SPRITES[2] -layer 16
RUN _TERM_SPRITE -x 180 -y 20 -sprite $SPRITES[2] -layer 16

RUN _TERM_RENDER -layer 16


# Draw Text

RUN _TERM_TEXT -x 60 -y 10 -text " --- Run Engineer, Run! --- " -color 18 -layer 10
RUN _TERM_TEXT -x 60 -y 20 -text "  Tap the left mouse click  " -color 18 -layer 10
RUN _TERM_TEXT -x 60 -y 160 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 60 -y 170 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 60 -y 180 -text "|" -color 18 -layer 10
RUN _TERM_TEXT -x 20 -y 180 -text "START" -color 18 -layer 10
RUN _TERM_TEXT -x 280 -y 160 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 280 -y 170 -text "|" -color 18 -layer 10 
RUN _TERM_TEXT -x 280 -y 180 -text "|FINISH" -color 18 -layer 10
RUN _TERM_RENDER -layer 10


# GAME LOOP:

RUN _TIMER --stop
RUN _TIMER --reset
RUN _TERM_MOUSE

WHILE ($X<290):
  
  # Clean the character old position
    
  IF ($X>40):
    RUN _TERM_CLEAN -x $X-$SPRITE_OFFSET -y $Y -width 40 -height 40 -layer 8
  END
  
  IF ($X>60 AND $TIMER_STATE==0):
    RUN _TIMER --start
    SET $TIMER_STATE = 1
  END
  
 
  # Get mouse events
    
  RUN _TERM_MOUSE TO $MOUSE_EVENTS
 
  
  # Update character position
  
  SET $ERROR = 0
  RUN _CALC ($MOUSE_EVENTS[0] - $X) TO $ERROR
  IF ($ERROR >= 0):
    RUN _CALC fmin($MOUSE_EVENTS[0]-$X, 1) TO $dV
  ELSE
    RUN _CALC fmax($MOUSE_EVENTS[0]-$X, -1) TO $dV
  END
  
  RUN _CALC (15 * $dV * $MOUSE_EVENTS[2]) TO $dV
  RUN _CALC floor(0.9*$VX + 0.1*$dV, 0) TO $VX
  
  SET $X = $X + $VX
  
  
  # Determine the active frame
  
  SET $FRAME_CHANGE = 0  
  IF ($MOUSE_EVENTS[2] > 0 AND $SPRITE_FRAME == 0):
    SET $SPRITE_FRAME = 1
    SET $FRAME_CHANGE = 1
  END
  
  IF ($MOUSE_EVENTS[2] > 0 AND $SPRITE_FRAME == 1 AND $FRAME_CHANGE == 0):
    SET $SPRITE_FRAME = 0
  EN
  
  # Draw the character new position
  
  RUN _TERM_SPRITE -x $X-$SPRITE_OFFSET -y $Y -sprite $SPRITES[$SPRITE_FRAME] -layer 8
  RUN _TERM_RENDER -layer 8

  
  # Cap framerate
  
  RUN _WAIT 66

END


# Display results
RUN _TIMER --stop
RUN _TIMER --get TO $TIME
RUN _CALC round($TIME / 1000, 2) TO $TIME
SET $STRING = "Timer: " + $TIME + " sec"

RUN _TERM_TEXT -x 105 -y 30 -text $STRING -color 18 -layer 9
RUN _TERM_RENDER -layer 9

RUN _WAIT 4000


  
# ==================== EXIT CLEANUP =======================


# Clear All Layers

FOR ($i=1;$i<17;$i++):
  RUN _TERM_CLEAN -x 0 -y 0 -width 640 -height 360 -layer $i
  RUN _TERM_RENDER -layer $i
END


# Press ENTER to exit

SET $KEY
INPUT $KEY
RUN _TERM_RESOLUTION 640 360
RUN _CLEAR
ECHO ON

